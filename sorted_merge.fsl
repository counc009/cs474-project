(Var x   Loc)
(Var y   Loc)
(Var ret Loc)

(Function next Loc Loc)
(Function key  Loc Int)

(RecFunction Keys   Loc SetInt)
(RecFunction Sorted Loc Bool)

(RecDef (Keys x) (ite (= x nil) EmptySetInt
                 (SetAdd (Keys (next x)) (key x))))
(RecDef (Sorted x) (ite (= x nil) True
                   (ite (= (next x) nil) True
                      (and (Sorted (next x))
                            (not (IsMember x (Sp (Sorted (antiSp (next x))))))
                            (<= (key x) (key (next x)))))))

(Var oldkeysx SetInt)
(Var oldkeysy SetInt)
(Var aux Loc)
(Var oldkeysaux SetInt)
(Var tmp Loc)

(Program sll_merge (x y oldkeysx oldkeysy ret))
(Pre (and (Sorted x) (Sorted y)))
(Post (and (Sorted ret) (= (Keys ret) (SetUnion oldkeysx oldkeysy))))

(assume (= oldkeysx (Keys x)))
(assume (= oldkeysy (Keys y)))

(If (= x nil)
 Then
  (assign ret y)
  (return)
 Else
  (If (= y nil)
   Then
    (assign ret x)
    (return)
   Else
    (If (<= (key x) (key y))
     Then
      (assign aux (next x))
      (assign oldkeysaux (Keys aux))
      (call sll_merge (aux y oldkeysaux oldkeysy tmp))
      (assign (next x) tmp)
      (assign ret x)
      (return)
     Else
      (assign aux (next y))
      (assign oldkeysaux (Keys aux))
      (call sll_merge (x aux oldkeysx oldkeysaux tmp))
      (assign (next y) tmp)
      (assign ret y)
      (return)
    )
  )
)
